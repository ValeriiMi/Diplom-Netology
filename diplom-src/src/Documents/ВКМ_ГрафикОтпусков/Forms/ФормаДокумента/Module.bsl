
#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	ОтпускаСотрудниковПриОкончанииРедактированияНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковПослеУдаления(Элемент)
	ОтпускаСотрудниковПриОкончанииРедактированияНаСервере();
КонецПроцедуры

#КонецОбласти



#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДиаграммаГанта(Команда)
	ДанныеФормы = Новый Структура;
	ДанныеФормы.Вставить("Адрес", ПоместитьВоВременноеХранилищеДанныеФормы());
	ДанныеФормы.Вставить("Год", Объект.Год);
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ДанныеФормы);
КонецПроцедуры

#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПоместитьВоВременноеХранилищеДанныеФормы()
	ТаблицаЗначений = Объект.ОтпускаСотрудников.Выгрузить();
	Адрес = ПоместитьВоВременноеХранилище(ТаблицаЗначений, УникальныйИдентификатор);
	Возврат Адрес;
КонецФункции

&НаСервере
Процедура ОтпускаСотрудниковПриОкончанииРедактированияНаСервере()
	
	Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник,
	|	СУММА(РАЗНОСТЬДАТ(ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаНачала,
	|		ВКМ_ГрафикОтпусковОтпускаСотрудников.ДатаОкончания, ДЕНЬ)) КАК КоличествоДнейОтпуска
	|ИЗ
	|	Документ.ВКМ_ГрафикОтпусков.ОтпускаСотрудников КАК ВКМ_ГрафикОтпусковОтпускаСотрудников
	|ГДЕ
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Ссылка = &Ссылка
	|СГРУППИРОВАТЬ ПО
	|	ВКМ_ГрафикОтпусковОтпускаСотрудников.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ПараметрыОтбора = Новый Структура("Сотрудник", Выборка.Сотрудник);
		НайденныеСтроки = Объект.ОтпускаСотрудников.НайтиСтроки(ПараметрыОтбора);
		
		Для Каждого Строка Из НайденныеСтроки Цикл
			Строка.КоличествоДнейОтпуска = Выборка.КоличествоДнейОтпуска;	
		КонецЦикла;
		
	КонецЦикла;
	
	Записать();

КонецПроцедуры


#КонецОбласти
