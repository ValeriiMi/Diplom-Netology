#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ДанныеОтпусков = Новый Массив;
	
	Для Каждого Отпуск Из ОтпускаСотрудников Цикл
	
		Если Месяц(Отпуск.ДатаНачала) = Месяц(Отпуск.ДатаОкончания) Тогда
			ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), Отпуск.ДатаНачала,
																								Отпуск.ДатаОкончания);
			ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
		Иначе
			ДатаНачала = Отпуск.ДатаНачала;
			ДатаОкончания = Отпуск.ДатаОкончания;
			Пока Месяц(ДатаНачала) <> Месяц(ДатаОкончания) Цикл
				КонецМесяца = КонецМесяца(ДатаНачала);
				ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), ДатаНачала,
																									КонецМесяца, 1);
				ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
				ДатаНачала = КонецМесяца + 1;
			КонецЦикла;	
			ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), ДатаНачала,
																 									ДатаОкончания);
			ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
		КонецЕсли;
		
	КонецЦикла;

	Движения.ВКМ_ЗапланированныеОтпуска.Записывать = Истина;
	
	Для Каждого Отпуск Из ДанныеОтпусков Цикл
	
		Движение = Движения.ВКМ_ЗапланированныеОтпуска.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Отпуск.Сотрудник;
		Движение.Год = Отпуск.Год;
		Движение.Месяц = Отпуск.Месяц;
		Движение.ДатаНачала = Отпуск.ДатаНачала;
		Движение.ДатаОкончания = Отпуск.ДатаОкончания;
			
	КонецЦикла;
	
	Движения.ВКМ_ЗапланированныеОтпуска.Записать();
	
	// Расчет количества дней отпуска сотрудников
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ЗапланированныеОтпуска.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ЗапланированныеОтпуска.Год КАК Год,
	|	ВКМ_ЗапланированныеОтпуска.Месяц КАК Месяц,
	|	ВКМ_ЗапланированныеОтпуска.ДатаНачала,
	|	ВКМ_ЗапланированныеОтпуска.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ДанныеОтпусков
	|ИЗ
	|	РегистрСведений.ВКМ_ЗапланированныеОтпуска КАК ВКМ_ЗапланированныеОтпуска
	|ГДЕ
	|	ВКМ_ЗапланированныеОтпуска.Регистратор = &Регистратор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ВКМ_РабочийГрафик.РабочийДень) КАК РабочийДень,
	|	ВТ_ДанныеОтпусков.НомерСтроки КАК НомерСтроки,
	|	ВТ_ДанныеОтпусков.Год КАК Год,
	|	ВТ_ДанныеОтпусков.Месяц КАК Месяц,
	|	ВТ_ДанныеОтпусков.ДатаНачала,
	|	ВТ_ДанныеОтпусков.ДатаОкончания
	|ПОМЕСТИТЬ ВТ_ОбщиеДанные
	|ИЗ
	|	ВТ_ДанныеОтпусков КАК ВТ_ДанныеОтпусков
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_РабочийГрафик КАК ВКМ_РабочийГрафик
	|		ПО (ВТ_ДанныеОтпусков.Год = ГОД(ВКМ_РабочийГрафик.Дата))
	|		И (ВТ_ДанныеОтпусков.Месяц = МЕСЯЦ(ВКМ_РабочийГрафик.Дата))
	|ГДЕ
	|	ВКМ_РабочийГрафик.РабочийДень = 0
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ДанныеОтпусков.НомерСтроки,
	|	ВТ_ДанныеОтпусков.Год,
	|	ВТ_ДанныеОтпусков.Месяц,
	|	ВТ_ДанныеОтпусков.ДатаНачала,
	|	ВТ_ДанныеОтпусков.ДатаОкончания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ОбщиеДанные.НомерСтроки КАК НомерСтроки,
	|	ВТ_ОбщиеДанные.Год КАК Год,
	|	ВТ_ОбщиеДанные.Месяц КАК Месяц,
	|	ВЫБОР
	|		КОГДА ВТ_ОбщиеДанные.РабочийДень <= 5
	|			ТОГДА 6
	|		ИНАЧЕ 5
	|	КОНЕЦ КАК РабочийДень,
	|	ВТ_ОбщиеДанные.ДатаНачала,
	|	ВТ_ОбщиеДанные.ДатаОкончания
	|ИЗ
	|	ВТ_ОбщиеДанные КАК ВТ_ОбщиеДанные";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.ВКМ_ЗапланированныеОтпуска[Выборка.НомерСтроки - 1];
		Движение.РабочихДней = Выборка.РабочийДень;
		
		КоличествоДнейОтпуска = 0;
		ДатаНачала = Выборка.ДатаНачала;
		ДатаОкончания = Выборка.ДатаОкончания;
		
		Пока ДатаНачала <= ДатаОкончания Цикл
			Если ДеньНедели(ДатаНачала) <= Выборка.РабочийДень Тогда
				КоличествоДнейОтпуска = КоличествоДнейОтпуска + 1;
			КонецЕсли;
			ДатаНачала = КонецДня(ДатаНачала) + 1;
		КонецЦикла;
		
		Движение.КоличествоДнейОтпуска = КоличествоДнейОтпуска;
		
	КонецЦикла;
	
	Движения.ВКМ_ЗапланированныеОтпуска.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьИнформациюОбОтпуске(Сотрудник, Год, ДатаНачала, ДатаОкончания, Доп = 0)
	
	ИнформацияОбОтпуске = Новый Структура;
	ИнформацияОбОтпуске.Вставить("Сотрудник", Сотрудник);
	ИнформацияОбОтпуске.Вставить("Год", Год);
	ИнформацияОбОтпуске.Вставить("Месяц", Месяц(ДатаНачала));
	ИнформацияОбОтпуске.Вставить("ДатаНачала", ДатаНачала);
	ИнформацияОбОтпуске.Вставить("ДатаОкончания", ДатаОкончания);
	
	Возврат ИнформацияОбОтпуске;
		
КонецФункции

#КонецОбласти

#КонецЕсли