#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	ДанныеОтпусков = Новый Массив;
	
	Для Каждого Отпуск Из ОтпускаСотрудников Цикл
	
		Если Месяц(Отпуск.ДатаНачала) = Месяц(Отпуск.ДатаОкончания) Тогда
			ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), Отпуск.ДатаНачала,
																								Отпуск.ДатаОкончания);
			ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
		Иначе
			ДатаНачала = Отпуск.ДатаНачала;
			ДатаОкончания = Отпуск.ДатаОкончания;
			Пока Месяц(ДатаНачала) <> Месяц(ДатаОкончания) Цикл
				КонецМесяца = КонецМесяца(ДатаНачала);
				ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), ДатаНачала,
																									КонецМесяца, 1);
				ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
				ДатаНачала = КонецМесяца + 1;
			КонецЦикла;	
			ИнформацияОбОтпуске = СформироватьИнформациюОбОтпуске(Отпуск.Сотрудник, Год(Год), ДатаНачала,
																 									ДатаОкончания);
			ДанныеОтпусков.Добавить(ИнформацияОбОтпуске);
		КонецЕсли;
		
	КонецЦикла;

	Движения.ВКМ_ЗапланированныеОтпуска.Записывать = Истина;
	
	Для Каждого Отпуск Из ДанныеОтпусков Цикл
	
		Движение = Движения.ВКМ_ЗапланированныеОтпуска.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Отпуск.Сотрудник;
		Движение.Год = Отпуск.Год;
		Движение.Месяц = Отпуск.Месяц;
		Движение.КоличествоДнейОтпуска = Отпуск.КоличествоДнейОтпуска;
			
	КонецЦикла;
	
	Движения.ВКМ_ЗапланированныеОтпуска.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СформироватьИнформациюОбОтпуске(Сотрудник, Год, ДатаНачала, ДатаОкончания, Доп = 0)
	
	ИнформацияОбОтпуске = Новый Структура;
	ИнформацияОбОтпуске.Вставить("Сотрудник", Сотрудник);
	ИнформацияОбОтпуске.Вставить("Год", Год);
	ИнформацияОбОтпуске.Вставить("Месяц", Месяц(ДатаНачала));
	
	КоличествоДнейОтпуска = 0;
	Пока ДатаНачала <= ДатаОкончания Цикл
		Если ДеньНедели(ДатаНачала) <> 7 Тогда
			КоличествоДнейОтпуска = КоличествоДнейОтпуска + 1;
		КонецЕсли;
		ДатаНачала = КонецДня(ДатаНачала) + 1;
	КонецЦикла;
	
	ИнформацияОбОтпуске.Вставить("КоличествоДнейОтпуска", КоличествоДнейОтпуска + Доп);
	
	Возврат ИнформацияОбОтпуске;
		
КонецФункции

#КонецОбласти

#КонецЕсли