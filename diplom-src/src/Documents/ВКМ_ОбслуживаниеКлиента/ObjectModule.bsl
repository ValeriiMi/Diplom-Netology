#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.ВидДоговора,
	|	ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия КАК ДатаНачалаДействия,
	|	ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия КАК ДатаОкончанияДействия,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &Договор";
	
	Запрос.УстановитьПараметр("Договор", Договор);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если Не (Выборка.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбоненскоеОбслуживание
		И Дата >= Выборка.ДатаНачалаДействия И Дата <= Выборка.ДатаОкончанияДействия) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Движения.ВКМ_ВыполненныеКлиентуРаботы.Записывать = Истина;
	
	КоличествоЧасов = 0;
	
	Для Каждого ТекСтрока Из ВыполненныеРаботы Цикл
		
		КоличествоЧасов = КоличествоЧасов + ТекСтрока.ЧасыКОплатеКлиенту;
		
	КонецЦикла;
	
	Движение = Движения.ВКМ_ВыполненныеКлиентуРаботы.Добавить();
	Движение.Период = Дата;
	Движение.Клиент = Клиент;
	Движение.Договор = Договор;
	Движение.КоличествоЧасов = КоличествоЧасов;
	Движение.СуммаКОплате = КоличествоЧасов * Выборка.СтоимостьЧасаРаботы;

	Запрос2 = Новый Запрос;
	Запрос2.Текст = "ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ПроцентОтРабот
	|ИЗ
	|	РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Период, Сотрудник = &Сотрудник) КАК
	|		ВКМ_УсловияОплатыСотрудниковСрезПоследних";
	
	Запрос2.УстановитьПараметр("Период", Дата);
	Запрос2.УстановитьПараметр("Сотрудник", Специалист);
	Выборка2 = Запрос2.Выполнить().Выбрать();
	Выборка2.Следующий();
	ПроцентОтРабот = Выборка2.ПроцентОтРабот;
	
	Если ПроцентОтРабот <> Неопределено Тогда
		Движения.ВКМ_ВыполненныеСотрудникомРаботы.Записывать = Истина;
		Движение = Движения.ВКМ_ВыполненныеСотрудникомРаботы.Добавить();
		Движение.Период = Дата;
		Движение.Сотрудник = Специалист;
		Движение.ЧасовКОплате = КоличествоЧасов;
		Движение.СуммаКОплате = КоличествоЧасов * Выборка.СтоимостьЧасаРаботы * ПроцентОтРабот / 100;
	Иначе
		Отказ = Истина;
		ОбщегоНазначения.СообщитьПользователю("На дату документа специалисту не установлен процент оплаты от работ");
		Возврат;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли